cmake_minimum_required(VERSION 3.15)

project(ElaFramework VERSION 0.1 LANGUAGES CXX)

set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
add_compile_options("$<$<CXX_COMPILER_ID:MSVC>:/utf-8>")

# 在这里配置 QT 路径（可选），例如 D:/Qt/6.6.2/msvc2019_64 或 /home/user/Qt/6.6.2/gcc_64。
set(QT_SDK_DIR "" CACHE PATH "Qt SDK directory override. Leave empty to auto-detect.")
set(CMAKE_INSTALL_PREFIX ${CMAKE_SOURCE_DIR}/Install CACHE PATH "Installation path" FORCE)

option(BUILD_ELAPACKETIO "Build ElaPacketIO" OFF)

if(NOT DEFINED Qt6_DIR AND DEFINED ENV{Qt6_DIR})
    set(Qt6_DIR $ENV{Qt6_DIR})
endif()
if(NOT DEFINED Qt5_DIR AND DEFINED ENV{Qt5_DIR})
    set(Qt5_DIR $ENV{Qt5_DIR})
endif()
if(NOT DEFINED QTDIR AND DEFINED ENV{QTDIR})
    set(QTDIR $ENV{QTDIR})
endif()

if(QT_SDK_DIR)
    list(APPEND CMAKE_PREFIX_PATH "${QT_SDK_DIR}")
elseif(DEFINED QTDIR)
    list(APPEND CMAKE_PREFIX_PATH "${QTDIR}")
endif()

if((NOT CMAKE_PREFIX_PATH) OR CMAKE_PREFIX_PATH STREQUAL "")
    if(QT_SDK_DIR)
        set(CMAKE_PREFIX_PATH "${QT_SDK_DIR}")
    elseif(DEFINED QTDIR)
        set(CMAKE_PREFIX_PATH "${QTDIR}")
    elseif(EXISTS "C:/Qt")
        file(GLOB _qt_dirs "C:/Qt/*")
        list(SORT _qt_dirs DESCENDING)
        foreach(_cand ${_qt_dirs})
            if(EXISTS "${_cand}/mingw_64")
                list(PREPEND CMAKE_PREFIX_PATH "${_cand}/mingw_64")
            elseif(EXISTS "${_cand}/msvc2019_64")
                list(PREPEND CMAKE_PREFIX_PATH "${_cand}/msvc2019_64")
            else()
                list(PREPEND CMAKE_PREFIX_PATH "${_cand}")
            endif()
        endforeach()
    endif()
endif()

find_package(QT NAMES Qt6 Qt5 COMPONENTS Widgets QUIET)

if(NOT QT_FOUND)
    message(WARNING "Unable to auto-detect Qt installation. Configure QT_SDK_DIR, set CMAKE_PREFIX_PATH, or define Qt6_DIR/Qt5_DIR.")
    find_package(QT NAMES Qt6 Qt5 REQUIRED COMPONENTS Widgets)
endif()

find_package(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS Widgets)

if(NOT QT_SDK_DIR)
    if(QT_VERSION_MAJOR EQUAL 6 AND DEFINED Qt6_DIR)
        get_filename_component(_qt_cmake_dir "${Qt6_DIR}" DIRECTORY)
        get_filename_component(_qt_lib_dir "${_qt_cmake_dir}" DIRECTORY)
        get_filename_component(_qt_root "${_qt_lib_dir}" DIRECTORY)
    elseif(QT_VERSION_MAJOR EQUAL 5 AND DEFINED Qt5_DIR)
        get_filename_component(_qt_cmake_dir "${Qt5_DIR}" DIRECTORY)
        get_filename_component(_qt_lib_dir "${_qt_cmake_dir}" DIRECTORY)
        get_filename_component(_qt_root "${_qt_lib_dir}" DIRECTORY)
    elseif(DEFINED Qt6_DIR)
        set(_qt_root ${Qt6_DIR})
    elseif(DEFINED Qt5_DIR)
        set(_qt_root ${Qt5_DIR})
    endif()
    if(_qt_root)
        set(QT_SDK_DIR "${_qt_root}" CACHE PATH "Qt SDK directory override. Leave empty to auto-detect." FORCE)
    endif()
endif()

message(STATUS "Using Qt SDK dir: ${QT_SDK_DIR}")

if (${QT_VERSION} VERSION_GREATER 6.7.0 OR ${QT_VERSION} VERSION_LESS 5.12.0)
    message("你当前使用的QT版本不在维护范围内！可能出现编译失败或使用时崩溃问题，推荐QT版本为QT5.15.2 QT6.6.2 QT6.6.3！请知悉~")
    message("The QT version you are currently using is no longer under maintenance! There may be issues such as compilation failures or crashes during use. The recommended QT versions are QT5.15.2, QT6.6.2, and QT6.6.3! Please be advised")
endif ()

if (${QT_VERSION} VERSION_GREATER_EQUAL 6.5.3 AND ${QT_VERSION} VERSION_LESS_EQUAL 6.6.1)
    message("你当前使用的QT版本为不推荐版本！可能出现使用时异常或崩溃问题，推荐QT版本为QT5.15.2 QT6.6.2 QT6.6.3！请知悉~")
    message("The QT version you are currently using is not recommended! It may cause exceptions or crashes during operation. The recommended QT versions are QT5.15.2, QT6.6.2, and QT6.6.3! Please be advised")
endif ()

if (NOT WIN32)
    add_link_options(-Wl,--disable-new-dtags)
    set(CMAKE_SKIP_INSTALL_RPATH FALSE)
    set(CMAKE_INSTALL_RPATH "${QT_SDK_DIR}/lib:${CMAKE_INSTALL_PREFIX}/ElaWidgetTools/lib")
endif ()

add_subdirectory(ElaWidgetTools)
if (WIN32 AND BUILD_ELAPACKETIO)
    add_definitions(-DBUILD_WITH_ELAPACKETIO)
    add_subdirectory(ElaPacketIO)
endif ()
add_subdirectory(ElaWidgetToolsExample)

if (${QT_VERSION} VERSION_LESS 6.1.0)
    set(BUNDLE_ID_OPTION MACOSX_BUNDLE_GUI_IDENTIFIER com.example.ElaWidgetTools)
endif ()
